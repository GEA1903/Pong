// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Pong/Ball.jack

/**
 * A graphical ball. Characterized by a screen location and distance of 
 * last destination. Has methods for drawing, erasing and moving on the screen.
 * The ball is displayed as a filled, 6-by-6 pixles rectangle. 
 */
class Ball {

    field int x, y;               // localização da bola na tela (in pixels)
    field int lengthx, lengthy;   // distância do último destino (in pixels)

    field int d, straightD, diagonalD;            // usado para computação de movimento em linha reta - Algoritmo de Bresenham
    field boolean invert, positivex, positivey;   // (mesmo uso)
   
    field int leftWall, rightWall, topWall, bottomWall;  // localização das paredes, determinando onde a bola pode se mover
   
    field int wall;   // última parede com a qual a bola colidiu

    /** Constrói uma nova bola com as localizações inicias e localizações das paredes dadas inicialmente */
    constructor Ball new(int Ax, int Ay,
                         int AleftWall, int ArightWall, int AtopWall, int AbottomWall) {    	
	    let x = Ax;		
	    let y = Ay;
	    let leftWall = AleftWall;
	    let rightWall = ArightWall - 6;    // -6 para o tamanho da bola
	    let topWall = AtopWall; 
	    let bottomWall = AbottomWall - 6;  // -6 para o tamanho da bola
	    let wall = 0;
        do show();
        return this;
    }

    /** Desloca a memória da bola */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Mostra a bola */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** Esconde a bola */
    method void hide() {
        do Screen.setColor(false);
	    do draw();
        return;
    }

    /** Desenha a bola */
    method void draw() {
	    do Screen.drawRectangle(x, y, x + 5, y + 5);
	    return;
    }

    /** Retorna a coordenada x (borda esquerda). */
    method int getLeft() {
        return x;
    }

    /** Returns x + 5 (borda direita, já que bola tem largura 6) */
    method int getRight() {
        return x + 5;
    }

    /** Define para onde a bola deve ir */
    method void setDestination(int destx, int desty) {
        var int dx, dy, temp;
  	    let lengthx = destx - x;
	    let lengthy = desty - y;
        let dx = Math.abs(lengthx);
        let dy = Math.abs(lengthy);
        let invert = (dx < dy);

        if (invert) {
            let temp = dx; // swap dx, dy
            let dx = dy;
            let dy = temp;
   	        let positivex = (y < desty);
            let positivey = (x < destx);
        }
        else {
	        let positivex = (x < destx);
            let positivey = (y < desty);
        }

        let d = (2 * dy) - dx;
        let straightD = 2 * dy;
        let diagonalD = 2 * (dy - dx);

	    return;
    }

    /**
     * Move a bola uma unidade mais próximo ao seu destino.
     * Se a bola atingir a parede, retorna 0.
     * Caso contrário, retorna um valor de acordo com a parede:
     * 1 (parede esquerda), 2 (parede direita), 3 (parede superior), 4 (parede inferior).
     */
    method int move() {

	    do hide();

        if (d < 0) { let d = d + straightD; }
        else {
            let d = d + diagonalD;

            if (positivey) {
                if (invert) { let x = x + 4; }
                else { let y = y + 4; }
            }
            else {
                if (invert) { let x = x - 4; }
                else { let y = y - 4; }
            }
	    }

        if (positivex) {
            if (invert) { let y = y + 4; }
            else { let x = x + 4; }
	    }
	    else {
            if (invert) { let y = y - 4; }
            else { let x = x - 4; }
	    }

	    if (~(x > leftWall)) {
	        let wall = 1;    
	        let x = leftWall;
	    }
        if (~(x < rightWall)) {
	        let wall = 2;    
	        let x = rightWall;
	    }
        if (~(y > topWall)) {
            let wall = 3;    
	        let y = topWall;
        }
        if (~(y < bottomWall)) {
            let wall = 4;    
	        let y = bottomWall;
        }

	    do show();

	    return wall;
    }

    /**
     * Calcula o novo destino da bola após rebater
     * parâmetros (-1/0/1=esquerda/centro/direita ou cima/centro/baixo).
     */
    method void bounce(int bouncingDirection) {
        var int newx, newy, divLengthx, divLengthy, factor;

	    // dividing by 10 first since results are too big
        let divLengthx = lengthx / 10;
        let divLengthy = lengthy / 10;
	    if (bouncingDirection = 0) { let factor = 10; }
	    else {
	        if (((~(lengthx < 0)) & (bouncingDirection = 1)) | ((lengthx < 0) & (bouncingDirection = (-1)))) {
                let factor = 20; // direção do rebote está na direção da bola
            }
	        else { let factor = 5; } // direção do rebote está contra a direção da bola
	    }

	    if (wall = 1) {
	        let newx = 506;
	        let newy = (divLengthy * (-50)) / divLengthx;
            let newy = y + (newy * factor);
	    }
        else {
            if (wall = 2) {
                let newx = 0;
                let newy = (divLengthy * 50) / divLengthx;
                let newy = y + (newy * factor);
	        }
	        else {
                if (wall = 3) {
		            let newy = 250;
		            let newx = (divLengthx * (-25)) / divLengthy;
                    let newx = x + (newx * factor);
		        }
                else { // assume wall = 4
		            let newy = 0;
		            let newx = (divLengthx * 25) / divLengthy;
                    let newx = x + (newx * factor);
		        }
            }
        }

        do setDestination(newx, newy);
        return;
    }
}
